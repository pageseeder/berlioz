plugins {
  id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

group       = 'org.pageseeder.berlioz'
version     = file('version.txt').text
description = "$title"

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  maven { url 'http://dl.bintray.com/pageseeder/maven' }
  jcenter()
}

dependencies {

  compile (
    'org.slf4j:slf4j-api:1.7.21',
    'org.pageseeder.xmlwriter:pso-xmlwriter:1.0.2'
  )

  // Expected to be provided by environment
  compileOnly (
    'javax.servlet:javax.servlet-api:3.1.0',
    'javax.json:javax.json-api:1.0'
  )

  testCompile (
    'junit:junit:4.12',
    'org.slf4j:slf4j-simple:1.7.21'
  )

}



publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourceJar {
        classifier "sources"
      }

      artifact javadocJar {
        classifier 'javadoc'
      }

      pom.withXml {
        asNode().children().last() + {
          resolveStrategy = Closure.DELEGATE_FIRST
          name        "$title"
          description "$description"
          url         "$website"

          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url  'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }

          organization {
            name 'Allette Systems'
            url  'https://www.allette.com.au'
          }
  
          scm {
            url                 "git@github.com:pageseeder/${gitName}.git"
            connection          "scm:git:git@github.com:pageseeder/${gitName}.git"
            developerConnection "scm:git:git@github.com:pageseeder/${gitName}.git"
          }

        }
      }

    }
  }
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

  // Reference to Maven config
  publications = ['mavenJava'] 

  dryRun = true
//  publish = true

  pkg {
    repo       = 'maven'
    name       = "${gitName}"
    userOrg    = 'pageseeder'
    desc       = "$description"
    websiteUrl = "$website"
    vcsUrl     = "https://github.com/pageseeder/${gitName}.git"
    licenses   = ['Apache-2.0']
    labels     = ['java', 'xslt', 'berlioz', 'xml']
    githubRepo = "pageseeder/${gitName}"
    githubReleaseNotesFile = 'README.md'

    version {
      name = "${project.version}"
    }
  }

}

task sourceJar(type: Jar) {
  description 'Packages up the source in a Jar for Maven publishing'
  from sourceSets.main.allJava
  from sourceSets.main.resources
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

task javadocJar(type: Jar, dependsOn: javadoc) {
  description 'Packages up the javadoc in a Jar for Maven publishing'
  from javadoc.destinationDir
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.1'
  description 'Generates the Gradle wrapper scripts'
}